plugins {
	id 'application'
	id 'checkstyle'
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.adarshr.test-logger' version '2.1.1'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'jacoco'
	id 'org.liquibase.gradle' version '2.0.4'
}

group = 'hexlet.code.javaproject73'
version = '1.0-SNAPSHOT'

application {
	mainClass = 'hexlet.code.javaproject73.App'
}

repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.projectlombok:lombok:1.18.22'

	annotationProcessor(
			'org.projectlombok:lombok:1.18.22',
			'com.querydsl:querydsl-apt:5.0.0:jpa',
			'javax.annotation:javax.annotation-api:1.3.2',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
	)
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation(
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-validation',
			'io.jsonwebtoken:jjwt:0.9.1',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0',
			'com.rollbar:rollbar-spring-boot-webmvc:1.8.1',
			'org.liquibase:liquibase-core:4.6.1',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0'
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core:4.6.1',
			'org.liquibase.ext:liquibase-hibernate5:4.5.0',
			'org.springframework.boot:spring-boot-starter-data-jpa'
	)

	runtimeOnly (
			'com.h2database:h2:1.4.200',
			'org.postgresql:postgresql:42.2.24'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test',
			'com.tobedevoured.modelcitizen:spring:0.8.3'
	)
}

wrapper {
	gradleVersion = '7.3'
	distributionType = Wrapper.DistributionType.ALL
}

compileJava {
	options.release = 16
	options.encoding = 'UTF-8'
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

test {
	useJUnitPlatform()
}

diffChangeLog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./taskManager'
			username ''
			password ''
			referenceUrl 'hibernate:spring:hexlet.code.javaproject73.model.' +
					// Указываем диалект
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}
